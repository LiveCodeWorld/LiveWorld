script "lib_Behavior"
--> MetaData
-
license: GPLv3
name: lib_Behavior
type: library
version: 0.4

/*
A library for script behaviors.
*/


--> Working on
-
getprop behavior_Hierarchy
   put the long id of the target into tObject
   put behavior_GetHierarchy (tObject) into bObjects
   return bObjects
end behavior_Hierarchy

setprop behavior_Hierarchy bObjects
   put the long id of the target into tObject
   --
   switch bObjects
      case "default"
         put the module_DefaultHierarchy of tObject into dHierarchy
         _ClearBehaviors dHierarchy
         _SetBehaviors tObject, dHierarchy
         put the result into setObjects
         break
      default
         behavior_SetHierarchy tObject,  bObjects
         put the result into setObjects
   end switch
   return setObjects
end behavior_Hierarchy

command behavior_SetHierarchy tObject, bObjects
   # Clear all behaviors to prevent behavior loop
   put behavior_GetHierarchy (tObject) into hObjects -- existing hierarchy
   _ClearHierarchyAndObjects hObjects, bObjects -- and clear the ones we are about to set
   
   # Clear the existing behavior hierarchy
   if bObjects is empty then return empty
   
   # Set behavior hierarchy
   _SetBehaviors tObject, bObjects
   --
   put the result into setObjects
   return setObjects
end behavior_SetHierarchy

private command _SetBehaviors tObject, bObjects
   if exists (tObject) is false then return empty
   put revRuggedId (tObject) into tObject
   
   # Set behavior objects
   repeat for each line bObject in bObjects
      put revRuggedId (bObject) into rObject
      
      switch
         case exists (rObject) is false
         case tObject = rObject
            next repeat
         default
            set the behavior of tObject to rObject
            put rObject into tObject
            put rObject & CR after setObjects
      end switch
   end repeat
   delete char -1 of setObjects
   return setObjects
end _SetBehaviors


--> Card | Behavior
-
command card_CreateBehavior bStackName
   put "lcw_Display" into projectName
   --
   put the project_Folder of stack projectName into projectFolder
   if bStackName is empty then
      ask_CreateProjectBehavior projectName
      put the result into bStackPath
      --
      display_Project projectName
      return bStackPath
   end if
   
   put project_ConstructStackPath (bStackName, projectFolder, "behaviors", "livecodescript") into newStackPath
   if exists (stack newStackPath) then
      ask_CreateProjectBehavior projectName
      put the result into bStackPath
   else
      project_CreateBehavior bName, projectName
      put the result into bStackPath
   end if
   --
   display_Project projectName
   return bStackPath
end card_CreateBehavior

function card_ListBehaviorNames
   put project_ListStackNames ("behaviors", "lcw_Display") into sNames
   filter sNames with "behavior_Card*"
   return sNames
end card_ListBehaviorNames


--> Props | Card
-
getprop card_BehaviorName
   put the card_Object of the target into cObject
   if exists (cObject) is false then return empty
   put the behavior of cObject into bObject
   if exists (bObject) is false then return empty
   
   put the short name of bObject into sName
   if char 1 to 13 of sName is "behavior_Card" then
      delete char 1 to 13 of sName
      return sName
   else
      return empty
   end if
end card_BehaviorName

setprop card_BehaviorName sName
   put "behavior_Card" & sName into stackName
   if exists (stack stackName) is false then
      put "behavior_CardMargin" into stackName
      if exists (stack stackName) is false then
         return empty
      end if
   end if
   put the long id of stack stackName into bObject
   --
   put the card_Object of the target into cObject
   if exists (cObject) is false then return empty
   --
   set the behavior of cObject to bObject
   return bObject
end card_BehaviorName


--> Module | Behaviors
-
command module_ClearDefaultBehaviors mObject
   put the module_DefaultHierarchy of mObject into bObjects
   _ClearBehaviors bObjects
end module_ClearDefaultBehaviors

command module_ClearBehaviors moduleName
   -- helps remove behavior loops
   put module_ListBehaviorObjects (moduleName) into bObjects
   -- put module_ListObjectAndBehaviors (moduleName) into bObjects
   
   _ClearBehaviors bObjects
end module_ClearBehaviors

command behavior_ClearHierarchy tObject, pMoreObjects
   put behavior_GetHierarchy (tObject) into bObjects
   _ClearHierarchyAndObjects bObjects, pMoreObjects
   put the result into hObjects
   return hObjects
end behavior_ClearHierarchy

private command _ClearHierarchyAndObjects bObjects, pMoreObjects
   repeat for each line sObject in pMoreObjects
      put revRuggedId (sObject) into rObject
      if rObject is not among the lines of bObjects then
         put CR & rObject after bObjects
      end if
   end repeat
   put word 1 to -1 of bObjects into bObjects
   --
   _ClearBehaviors bObjects
   return bObjects
   
end _ClearHierarchyAndObjects

private command _ClearBehaviors bObjects
   -- not guaranteed to remove behavior loops
   repeat for each line bObject in bObjects
      if exists (bObject) then
         set the behavior of bObject to empty
      end if
   end repeat
end _ClearBehaviors


--> Behavior | Hierarchy
-
getprop behavior_InHierarchy [stackName]
   put the long id of the target into tObject
   if exists (stack stackName) is false then return merge ("Error, stack '[[stackName]]' does nto exists.")
   --
   put the behavior_Hierarchy of tObject into bObjects
   put the name of stack stackName into stackObject
   return stackObject is among the lines of bObjects
end behavior_InHierarchy

function behavior_GetHierarchy tObject, pUseRugged
   repeat
      put the behavior of tObject into bObject
      if exists (bObject) is false then exit repeat
      if pUseRugged is not false then
         put revRuggedId (bObject) into bObject
      end if
      put bObject & CR after bObjects
      put bObject into tObject
   end repeat
   delete char -1 of bObjects
   return bObjects
end behavior_GetHierarchy


--> Behavior | Add
-
setprop behavior_Remove stackName
   if exists (stack stackName) is false then return merge ("Error, stack '[[stackName]]' does nto exists.")
   --
   put the behavior_Hierarchy of the target into bObjects
   put the name of stack stackName into stackObject
   line_Delete stackObject, bObjects
   set the behavior_Hierarchy of the target to bObjects
   --
   return bObjects
end behavior_Remove

setprop behavior_Add stackName
   if exists (stack stackName) is false then return merge ("Error, stack '[[stackName]]' does nto exists.")
   --
   put the behavior_Hierarchy of the target into bObjects
   put the name of stack stackName into stackObject
   line_Add stackObject, bObjects
   set the behavior_Hierarchy of the target to bObjects
   --
   return bObjects
end behavior_Add


--> Behavior
-
setprop behavior_ClearNames stackNames
   if stackNames is empty then return empty
   replace comma with CR in stackNames
   put the long id of the target into tObject
   --
   repeat for each line stackName in stackNames
      if exists (stack stackName) is false then next repeat
      put the long id of stack stackName into bObject
      if tObject = bObject then next repeat
      --
      put the name of stack stackName into sObject
      set the behavior of sObject to empty
   end repeat
end behavior_ClearNames

getprop behavior_StackNames
   put the long id of the target into tObject
   repeat
      put the behavior of tObject into bObject
      switch
         case word 1 of bObject is not "stack"
         case exists (bObject) is false
            delete char -1 of bStackNames
            return bStackNames
         default
            put the stack_Name of bObject into bStackName
            put bStackName & CR after bStackNames
            put bObject into tObject
      end switch
   end repeat
end behavior_StackNames

setprop behavior_StackNames stackNames
   if stackNames is empty then return empty
   replace comma with CR in stackNames
   put the long id of the target into tObject
   
   # Clear any behaviors (or we might cause a loop)
   behavior_ClearHierarchy tObject
   
   repeat for each line stackName in stackNames
      if exists (stack stackName) is false then next repeat
      put the name of stack stackName into bObject
      if tObject = bObject then next repeat
      
      put the behavior of tObject into tBehavior
      put the behavior of bObject into bBehavior
      --
      set the behavior of tObject to bObject
      put bObject into tObject
      put bObject & CR after bObjects
   end repeat
   delete char -1 of bObjects
   return bObjects
end behavior_StackNames

setprop behavior_StackNames bStackNames
   -- test and refactor
   put the long id of the target into tObject
   --
   replace comma with CR in bStackNames
   repeat for each line stackName in bStackNames
      if exists (stack stackName) is false then exit repeat
      put the stack_Object of stack stackName & CR after bObjects
   end repeat
   delete char -1 of bObjects
   --
   behavior_SetHierarchy tObject,  bObjects
   return the result
end behavior_StackNames


--> Behavior | StackName
-
getprop behavior_StackName
   put the behavior of the target into bObject
   switch
      case word 1 of bObject is not "stack"
      case exists (bObject) is false
         return empty
      default
         put the stack_Name of bObject into behaviorStackName
         return behaviorStackName
   end switch
end behavior_StackName

setprop behavior_StackName stackName
   if exists (stack stackName) is false then return empty
   put the name of stack stackName into stackObject
   set the behavior of the target to stackObject
   return stackObject
end behavior_StackName


--> Behavior
-
getprop behavior_HandlerExists [hkeyLine]
   put the behavior of the target into bObject
   if exists (bObject) is false then return empty
   --
   put the revAvailablehandlers of bObject into hTable
   htable_ExactLineOffsets hkeyLine, hTable, startLineNum, endLineNum
   return the result
end behavior_HandlerExists

getprop behavior_Handler [hkeyLine]
   put the behavior of the target into bObject
   if exists (bObject) is false then return empty
   --
   put the revAvailablehandlers of bObject into hTable
   htable_ExactLineOffsets hkeyLine, hTable, startLineNum, endLineNum
   put line startLineNum to endLineNum of the script of bObject into behaviorHandler
   return behaviorHandler
end behavior_Handler


--> Behavior | Hierarchy
-
getprop behavior_Objects [useRugged]
   put the long id of the target into tObject
   put behavior_GetHierarchy (tObject, useRugged) into bObjects
   return bObjects
end behavior_Objects

setprop behavior_Objects bObjects
   put the long id of the target into tObject
   behavior_SetHierarchy tObject,  bObjects
   return the result
end behavior_Objects

getprop behavior_LastObject
   return the last line of the behavior_Objects of the target
end behavior_LastObject
